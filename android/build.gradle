group 'com.codebumble.tflite_plus'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.9.20'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.codebumble.tflite_plus'
    }

    // Use the compileSdkVersion from the app if available, otherwise default to latest
    def compileSdkVersionOverride = project.hasProperty('android.compileSdkVersion') ? 
        Integer.parseInt(project.property('android.compileSdkVersion')) : null
    compileSdk compileSdkVersionOverride ?: 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
        // Use the targetSdkVersion from the app if available, otherwise use compileSdk
        def targetSdkVersionOverride = project.hasProperty('android.targetSdkVersion') ? 
            Integer.parseInt(project.property('android.targetSdkVersion')) : null
        targetSdkVersion targetSdkVersionOverride ?: (compileSdkVersionOverride ?: 36)
    }

    dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // --- LiteRT (runtime + support + delegates) ---
    implementation 'com.google.ai.edge.litert:litert:1.4.0'
    implementation 'com.google.ai.edge.litert:litert-api:1.4.0'          // ensures API present from LiteRT
    implementation 'com.google.ai.edge.litert:litert-support:1.4.0'
    implementation 'com.google.ai.edge.litert:litert-support-api:1.4.0'
    implementation 'com.google.ai.edge.litert:litert-gpu:1.4.0'
    implementation 'com.google.ai.edge.litert:litert-gpu-api:1.4.0'
    implementation 'com.google.ai.edge.litert:litert-metadata:1.4.0'

    // --- Task Library (keep the API, but EXCLUDE original TF Lite + Support that it brings) ---
    implementation('org.tensorflow:tensorflow-lite-task-vision:0.4.4') {
        exclude group: 'org.tensorflow', module: 'tensorflow-lite'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-api'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-gpu'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-gpu-api'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-metadata'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-support'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-support-api'
    }
}

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}